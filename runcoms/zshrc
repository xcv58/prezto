#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   xcv58 <i@xcv58.com>
#

function remove_path ()  {
  # Pure zsh implementation (no external processes for speed)
  local -a paths
  paths=("${(@s/:/)PATH}")
  paths=(${paths:#$1})
  export PATH="${(j/:/)paths}"
}

function insert_path () {
  remove_path "${*}"
  [[ -d "${*}" ]] && export PATH=${*}:${PATH}
}

function append_path () {
  remove_path "${*}"
  [[ -d "${*}" ]] && export PATH=${PATH}:${*}
}

function source_file() {
  # shellcheck disable=SC1090
  [[ -s "${*}" ]] && source "${*}"
}

function exportENV() {
  [[ -d $2 ]] && {
    export ${1}=${2}
  }
}

# clear Docker container by name
function clearContainer() {
  docker ps -q --filter "name=${1}" | xargs -n 1 docker stop
  docker ps -a -q --filter "name=${1}" | xargs -n 1 docker rm
}

# TODO: need check whether is has 256color support
if [[ "$TERM" == "xterm" ]]; then
  export TERM=xterm-256color
fi

# Source Prezto.
source_file "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"

# Customize to your needs...

# for Emacs and EmacsClient
alias ec="emacsclient -n"

export ALTERNATE_EDITOR="vim"

# Path to set history to ~/.zhistory
export HISTFILE="${ZDOTDIR:-$HOME}/.zhistory"

# Path for groovy
exportENV GROOVY_HOME /usr/local/opt/groovy/libexec

# Path for /usr/local/bin
insert_path /usr/local/bin

# Path for go lib
insert_path /usr/local/opt/go/libexec/bin/bin

# add current directory to PATH
export PATH=.:${PATH}

# Path for bins
export PATH=${PATH}:/usr/bin:/bin:/usr/sbin:/sbin:/usr/texbin

# Path for Android
[[ -d ~/SDK-Android ]] && {
  export ANDROID_HOME=~/SDK-Android
  append_path ${ANDROID_HOME}
  append_path ${ANDROID_HOME}/platform-tools
}

exportENV ANDROID_NDK_HOME ~/Library/Android/android-ndk

# Set Gradle_HOME
exportENV GRADLE_HOME /usr/local/Cellar/gradle/2.6

# HADOOP related variables and Path
[[ -d ~/hadoop ]] && {
  export HADOOP_INSTALL=~/hadoop
  export HADOOP_HOME=$HADOOP_INSTALL
  export PATH=${PATH}:$HADOOP_INSTALL/bin:$HADOOP_INSTALL/sbin
  export HADOOP_MAPRED_HOME=$HADOOP_INSTALL
  export HADOOP_COMMON_HOME=$HADOOP_INSTALL
  export HADOOP_HDFS_HOME=$HADOOP_INSTALL
  export YARN_HOME=$HADOOP_INSTALL
}

# For sed error: illegal byte sequence. via http://stackoverflow.com/questions/19242275/re-error-illegal-byte-sequence-on-mac-os-x
export LC_CTYPE=C
export LANG=C

# Fix jekyll "invalid byte sequence in US-ASCII" error
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

# for emacs term
if [[ -n ${INSIDE_EMACS} ]]; then
  # This shell runs inside an Emacs *shell*/*term* buffer.
  prompt walters
  unsetopt zle
fi

# Path for RVM
append_path "${HOME}/.rvm/bin"

# node path
exportENV NODE_PATH /usr/local/lib/node_modules

# for OS161 CSE 521
[[ -d ~/root && -d ~/os161 && -d ~/src ]] && {
  alias db="cd ~/root && mips-harvard-os161-gdb kernel"
  append_path ~/os161/tools/bin
}

# Vi Mode
source_file "${ZDOTDIR:-$HOME}/.zshrc_vimode.zsh"

# for alias
source_file "${ZDOTDIR:-$HOME}/.zshrc_alias.zsh"

# for AppleScript Alias
source_file "${ZDOTDIR:-$HOME}/.zshrc_scpt_alias.zsh"

# # local config
source_file "${ZDOTDIR:-$HOME}/.zshrc_local"

# Go Version Manager (lazy-loaded)
function load_gvm() {
    source_file "${ZDOTDIR:-$HOME}/.gvm/scripts/gvm"
}
lazy_load load_gvm go gvm

# Google Cloud SDK (lazy-loaded)
function load_gcloud() {
    source_file "${ZDOTDIR:-$HOME}/google-cloud-sdk/path.zsh.inc"
    source_file "${ZDOTDIR:-$HOME}/google-cloud-sdk/completion.zsh.inc"
}
lazy_load load_gcloud gcloud gsutil bq

# zsh-abbr config - store in repo for portability
export ABBR_USER_ABBREVIATIONS_FILE="${ZDOTDIR:-$HOME}/.zprezto/config/zsh-abbr-user-abbreviations"

# zsh-abbr (fast cache-based loading)
# Full zsh-abbr takes ~600ms due to parsing. This uses cached arrays (~10ms)
function _load_abbr_fast() {
    local cache_dir="${TMPDIR:-/tmp}/zsh-abbr"
    local user_file="$ABBR_USER_ABBREVIATIONS_FILE"

    # Check if cache exists and is valid
    if [[ -f "$cache_dir/regular-user-abbreviations" ]]; then
        # Load cached arrays directly (instant!)
        source "$cache_dir/regular-user-abbreviations" 2>/dev/null
        source "$cache_dir/global-user-abbreviations" 2>/dev/null

        # Self-healing: if cache is empty but user file has content, regenerate
        if (( ${#ABBR_REGULAR_USER_ABBREVIATIONS} == 0 )) && [[ -s "$user_file" ]]; then
            rm -rf "$cache_dir"
            source_file "${ZDOTDIR:-$HOME}/.zprezto/zsh-abbr/zsh-abbr.zsh"
            return
        fi

        # Set up minimal expansion widget (the key feature of zsh-abbr)
        function _abbr_expand_widget() {
            local abbr="${LBUFFER##* }"
            local expansion="${ABBR_REGULAR_USER_ABBREVIATIONS[\"$abbr\"]:-${ABBR_GLOBAL_USER_ABBREVIATIONS[\"$abbr\"]}}"
            if [[ -n "$expansion" ]]; then
                LBUFFER="${LBUFFER%$abbr}${(Q)expansion}"
            fi
        }
        zle -N _abbr_expand_widget

        # Bind space to expand (like zsh-abbr does)
        function _abbr_expand_and_space() {
            _abbr_expand_widget
            LBUFFER+=" "
        }
        zle -N _abbr_expand_and_space
        bindkey " " _abbr_expand_and_space

        # For full abbr command functionality, lazy-load full plugin
        function abbr() {
            unfunction abbr
            source "${ZDOTDIR:-$HOME}/.zprezto/zsh-abbr/zsh-abbr.zsh"
            abbr "$@"
        }
    else
        # No cache - fall back to full load (first time only)
        source_file "${ZDOTDIR:-$HOME}/.zprezto/zsh-abbr/zsh-abbr.zsh"
    fi
}

# Load on 2nd prompt for instant first prompt
_abbr_load_count=0
function _load_abbr_on_second_prompt() {
    (( _abbr_load_count++ ))
    if (( _abbr_load_count >= 2 )); then
        _load_abbr_fast
        add-zsh-hook -d precmd _load_abbr_on_second_prompt
        unset _abbr_load_count
    fi
}
autoload -Uz add-zsh-hook
add-zsh-hook precmd _load_abbr_on_second_prompt

export GPG_TTY=$(tty)

# iTerm2 shell integration
source_file "${ZDOTDIR:-$HOME}/.iterm2_shell_integration.zsh"

# fzf
source_file "${ZDOTDIR:-$HOME}/.fzf.zsh"

function xcv58-clear-screen () {
    clear
    zle reset-prompt
}
zle -N xcv58-clear-screen

insert_path "${ZDOTDIR:-$HOME}/stripe/work/exe"
insert_path "${ZDOTDIR:-$HOME}/stripe/space-commander/bin"
insert_path "/opt/homebrew/bin"
insert_path "${ZDOTDIR:-$HOME}/.local/bin"
insert_path "${ZDOTDIR:-$HOME}/.antigravity/antigravity/bin"

# for Mac OS only
[[ $(uname) == "Darwin" ]] && source_file "${ZDOTDIR:-$HOME}/.zshrc_mac.zsh"

# pnpm
exportENV PNPM_HOME "${ZDOTDIR:-$HOME}/Library/pnpm"
insert_path "${PNPM_HOME}"

# nvm (lazy-loaded for fast startup)
export NVM_DIR="$HOME/.nvm"
function load_nvm() {
    [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"
    [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"
}
lazy_load load_nvm nvm node npm npx yarn pnpm corepack codex
