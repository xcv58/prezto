#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   xcv58 <i@xcv58.com>
#

typeset -gU path

function remove_path ()  {
  local target="${*}"
  path=(${path:#$target})
}

function insert_path () {
  local target="${*}"
  [[ -d "${target}" ]] || return 0
  path=("${target}" ${path:#$target})
}

function append_path () {
  local target="${*}"
  [[ -d "${target}" ]] || return 0
  path=(${path:#$target} "${target}")
}

function source_file() {
  # shellcheck disable=SC1090
  [[ -s "${*}" ]] && source "${*}"
}

typeset -gA _lazy_loaded

function _lazy_source() {
  local key="${1}"
  local file="${2}"
  if [[ -z "${_lazy_loaded[$key]}" ]]; then
    _lazy_loaded[$key]=1
    [[ -s "${file}" ]] && source "${file}"
  fi
}

function _lazy_run() {
  local key="${1}"
  shift
  if [[ -z "${_lazy_loaded[$key]}" ]]; then
    _lazy_loaded[$key]=1
    eval "${*}"
  fi
}

function _lazy_cmd() {
  local cmd="${1}"
  local loader="${2}"
  eval "function ${cmd}() { unset -f ${cmd}; ${loader}; if whence -w ${cmd} >/dev/null 2>&1; then eval \"${cmd} \\\"\\$@\\\"\"; else command ${cmd} \"\\$@\"; fi }"
}

function _lazy_cmd_optional() {
  local cmd="${1}"
  local loader="${2}"
  local message="${3}"
  eval "function ${cmd}() { unset -f ${cmd}; ${loader}; if whence -w ${cmd} >/dev/null 2>&1; then eval \"${cmd} \\\"\\$@\\\"\"; else print -u2 \"${message}\"; return 127; fi }"
}

function _lazy_eval() {
  local key="${1}"
  shift
  if [[ -z "${_lazy_loaded[$key]}" ]]; then
    _lazy_loaded[$key]=1
    eval "${*}"
  fi
}

autoload -Uz add-zsh-hook

function exportENV() {
  [[ -d $2 ]] && {
    export ${1}=${2}
  }
}

# clear Docker container by name
function clearContainer() {
  docker ps -q --filter "name=${1}" | xargs -n 1 docker stop
  docker ps -a -q --filter "name=${1}" | xargs -n 1 docker rm
}

# TODO: need check whether is has 256color support
if [[ "$TERM" == "xterm" ]]; then
  export TERM=xterm-256color
fi

# Source Prezto.
source_file "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"

# Customize to your needs...

# for Emacs and EmacsClient
alias ec="emacsclient -n"

export ALTERNATE_EDITOR="vim"

# Path to set history to ~/.zhistory
export HISTFILE="${ZDOTDIR:-$HOME}/.zhistory"

# Path for groovy
exportENV GROOVY_HOME /usr/local/opt/groovy/libexec

# Path for /usr/local/bin
insert_path /usr/local/bin

# Path for go lib
insert_path /usr/local/opt/go/libexec/bin/bin

# add current directory to PATH
export PATH=.:${PATH}

# Path for bins
export PATH=${PATH}:/usr/bin:/bin:/usr/sbin:/sbin:/usr/texbin

# Path for Android
[[ -d ~/SDK-Android ]] && {
  export ANDROID_HOME=~/SDK-Android
  append_path ${ANDROID_HOME}
  append_path ${ANDROID_HOME}/platform-tools
}

exportENV ANDROID_NDK_HOME ~/Library/Android/android-ndk

# Set Gradle_HOME
exportENV GRADLE_HOME /usr/local/Cellar/gradle/2.6

# HADOOP related variables and Path
[[ -d ~/hadoop ]] && {
  export HADOOP_INSTALL=~/hadoop
  export HADOOP_HOME=$HADOOP_INSTALL
  export PATH=${PATH}:$HADOOP_INSTALL/bin:$HADOOP_INSTALL/sbin
  export HADOOP_MAPRED_HOME=$HADOOP_INSTALL
  export HADOOP_COMMON_HOME=$HADOOP_INSTALL
  export HADOOP_HDFS_HOME=$HADOOP_INSTALL
  export YARN_HOME=$HADOOP_INSTALL
}

# For sed error: illegal byte sequence. via http://stackoverflow.com/questions/19242275/re-error-illegal-byte-sequence-on-mac-os-x
export LC_CTYPE=C
export LANG=C

# Fix jekyll "invalid byte sequence in US-ASCII" error
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

# for emacs term
if [[ -n ${INSIDE_EMACS} ]]; then
  # This shell runs inside an Emacs *shell*/*term* buffer.
  prompt walters
  unsetopt zle
fi

# Path for RVM
append_path "${HOME}/.rvm/bin"

# node path
exportENV NODE_PATH /usr/local/lib/node_modules

# for OS161 CSE 521
[[ -d ~/root && -d ~/os161 && -d ~/src ]] && {
  alias db="cd ~/root && mips-harvard-os161-gdb kernel"
  append_path ~/os161/tools/bin
}

# Vi Mode
source_file "${ZDOTDIR:-$HOME}/.zshrc_vimode.zsh"

# for alias
source_file "${ZDOTDIR:-$HOME}/.zshrc_alias.zsh"

# for AppleScript Alias
source_file "${ZDOTDIR:-$HOME}/.zshrc_scpt_alias.zsh"

# # local config
source_file "${ZDOTDIR:-$HOME}/.zshrc_local"

# The next line updates PATH for the Google Cloud SDK.
function _load_gcloud() {
  _lazy_source gcloud_path "${ZDOTDIR:-$HOME}/google-cloud-sdk/path.zsh.inc"
  _lazy_source gcloud_completion "${ZDOTDIR:-$HOME}/google-cloud-sdk/completion.zsh.inc"
}
_lazy_cmd_optional gcloud _load_gcloud "gcloud: not found or gcloud init failed"
_lazy_cmd_optional gsutil _load_gcloud "gsutil: not found or gcloud init failed"
_lazy_cmd_optional bq _load_gcloud "bq: not found or gcloud init failed"

# Go Version Manager
function _load_gvm() {
  _lazy_source gvm "${ZDOTDIR:-$HOME}/.gvm/scripts/gvm"
}
_lazy_cmd_optional gvm _load_gvm "gvm: not found or gvm init failed"

# zsh-abbr
autoload -Uz add-zle-hook-widget
function _load_abbr() {
  if [[ -z "${_lazy_loaded[abbr]}" ]]; then
    _lazy_loaded[abbr]=1
    _lazy_source abbr "${ZDOTDIR:-$HOME}/.zprezto/zsh-abbr/zsh-abbr.zsh"
  fi
}
function _abbr_lazy_init() {
  _load_abbr
  add-zle-hook-widget -d zle-line-init _abbr_lazy_init
}
add-zle-hook-widget zle-line-init _abbr_lazy_init

export GPG_TTY=$(tty)

if [ -f "${ZDOTDIR:-$HOME}/.iterm2_shell_integration.zsh" ]; then source "${ZDOTDIR:-$HOME}/.iterm2_shell_integration.zsh"; fi

function _load_fzf() {
  _lazy_source fzf "${ZDOTDIR:-$HOME}/.fzf.zsh"
}
_lazy_cmd_optional fzf _load_fzf "fzf: not found or fzf init failed"

function xcv58-clear-screen () {
    clear
    zle reset-prompt
}
zle -N xcv58-clear-screen

insert_path "${ZDOTDIR:-$HOME}/stripe/work/exe"
insert_path "${ZDOTDIR:-$HOME}/stripe/space-commander/bin"
insert_path "/opt/homebrew/bin"
insert_path "${ZDOTDIR:-$HOME}/.local/bin"

# for Mac OS only
[[ $(uname) == "Darwin" ]] && source_file "${ZDOTDIR:-$HOME}/.zshrc_mac.zsh"

# Lazy-loaded above; keep this line disabled to avoid startup cost.
# [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
# eval "$(rbenv init - zsh)"

# pnpm
exportENV PNPM_HOME "${ZDOTDIR:-$HOME}/Library/pnpm"
insert_path "${PNPM_HOME}"

# nvm
export NVM_DIR="$HOME/.nvm"
function _load_nvm() {
  _lazy_source nvm "${NVM_DIR}/nvm.sh"
  _lazy_source nvm_completion "${NVM_DIR}/bash_completion"
}
_lazy_cmd_optional nvm _load_nvm "nvm: not found or nvm init failed"

function _load_conda() {
  local conda_bin="/Users/xcv58/miniconda3/bin/conda"
  if [[ -x "${conda_bin}" ]]; then
    __conda_setup="$("${conda_bin}" 'shell.zsh' 'hook' 2> /dev/null)"
    if [[ $? -eq 0 ]]; then
      eval "$__conda_setup"
    else
      if [[ -f "/Users/xcv58/miniconda3/etc/profile.d/conda.sh" ]]; then
        . "/Users/xcv58/miniconda3/etc/profile.d/conda.sh"
      else
        export PATH="/Users/xcv58/miniconda3/bin:$PATH"
      fi
    fi
    unset __conda_setup
  fi
}
_lazy_cmd_optional conda _load_conda "conda: not found or conda init failed"
_lazy_cmd_optional mamba _load_conda "mamba: not found or conda init failed"


# bun completions
function _load_bun_completion() {
  _lazy_source bun_completion "/Users/xcv58/.bun/_bun"
}
_lazy_cmd_optional bun _load_bun_completion "bun: not found or bun completion init failed"

# bun
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"

# Added by Antigravity
export PATH="/Users/xcv58/.antigravity/antigravity/bin:$PATH"

# OpenClaw Completion
function _load_openclaw_completion() {
  _lazy_run openclaw_completion "source <(openclaw completion --shell zsh)"
}
_lazy_cmd_optional openclaw _load_openclaw_completion "openclaw: not found or openclaw completion init failed"
